# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Dan Johansen <strit@manjaro.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Removed Gallium3D drivers/packages for chipsets that don't exist in our ARM devices (intel, VMware svga).
#  - added broadcom and panfrost vulkan packages
#  - enable lto for aarch64
#  ALARM: John Audia <therealgraysky@proton.me>
#  - added freedreno driver, PR#1973 and PR#1996

highmem=1

pkgbase=mesa
pkgname=(
  mesa
  libva-mesa-driver
  mesa-vdpau
  opencl-clover-mesa
  # opencl-rusticl-mesa
  vulkan-mesa-layers
  # vulkan-nouveau
  # vulkan-radeon
  vulkan-swrast
  vulkan-virtio
  # vulkan-broadcom
  # vulkan-panfrost
  vulkan-freedreno
)
_commit=main
pkgver=24.2.0_pre
pkgrel=1
epoch=1
pkgdesc="Open-source OpenGL drivers"
url="https://www.mesa3d.org/"
arch=(x86_64 aarch64)
license=("MIT AND BSD-3-Clause AND SGI-B-2.0")
makedepends=(
  clang
  expat
  gcc-libs
  glibc
  libdrm
  libelf
  libglvnd
  libva
  libvdpau
  libx11
  libxcb
  libxext
  libxfixes
  libxml2
  libxrandr
  libxshmfence
  libxxf86vm
  llvm
  llvm-libs
  lm_sensors
  rust
  spirv-llvm-translator
  spirv-tools
  systemd-libs
  vulkan-icd-loader
  wayland
  xcb-util-keysyms
  zlib
  zstd

  # shared between mesa and lib32-mesa
  cbindgen
  clang
  cmake
  elfutils
  glslang
  libclc
  meson
  python-mako
  python-packaging
  python-ply
  rust-bindgen
  wayland-protocols
  xorgproto

  # valgrind deps
  valgrind

  # d3d12 deps
  directx-headers

  # gallium-omx deps
  libomxil-bellagio

  # etnaviv deps
  python-pycparser
)
options=(
  # GCC 14 LTO causes segfault in LLVM under si_llvm_optimize_module
  # https://gitlab.freedesktop.org/mesa/mesa/-/issues/11140
  #
  # In general, upstream considers LTO to be broken until explicit notice.
  !lto
)
source=(https://gitlab.freedesktop.org/mesa/mesa/-/archive/$_commit/mesa-$_commit.tar.gz
        )
        b2sums=('0073c58a0455c5d67613557a844da5190c9059b94e56ad7b41cccaa1a68cedc3cbdac8131effcfdb84e793987546072e5de7dba151df6c09158288b305605625')

# https://docs.mesa3d.org/relnotes.html
sha256sums=('4f25b1698fb48e63581d2f36f3ce11ef8d159d2044a2811dfd9b1da5e079ba0a')

prepare() {
  mv mesa-$_commit mesa-$pkgver
  cd mesa-$pkgver
}

build() {
  case "${CARCH}" in
    armv7h)  GALLIUM=",etnaviv,kmsro,lima,panfrost,tegra,v3d,vc4" ;;
    aarch64) GALLIUM=",kmsro" ;;
  esac

  local meson_options=(
    -D android-libbacktrace=disabled
    #-D b_lto=$([[ $CARCH == aarch64 ]] && echo true || echo false)
    -D b_ndebug=true
    -D gallium-drivers=freedreno,swrast,virgl,zink
    -D gallium-extra-hud=true
    -D gallium-va=enabled
    -D gallium-vdpau=enabled
    -D gallium-nine=true
    -D gallium-opencl=icd
    -D gallium-rusticl=false
    -D gallium-xa=disabled
    -D gles1=disabled
    -D glx=dri
    -D intel-clc=auto
    -D intel-rt=disabled
    -D libunwind=disabled
    -D microsoft-clc=disabled
    -D osmesa=true
    -D platforms=x11,wayland
    -D valgrind=enabled
    -D video-codecs=all
    -D vulkan-drivers=swrast,freedreno,virtio
    -D vulkan-layers=device-select,overlay
  )

  # Build only minimal debug info to reduce size
  CFLAGS+=" -g1"
  CXXFLAGS+=" -g1"

  # Inject subproject packages
  export MESON_PACKAGE_CACHE_DIR="$srcdir"

  arch-meson mesa-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    echo "PWD=$(pwd)"
    echo "mv $f $d"
    mkdir -p "$(dirname "$d")"
    mv -v "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mesa() {
  depends=(
    expat
    gcc-libs
    glibc
    libdrm
    libelf
    libglvnd
    libx11
    libxcb
    libxext
    libxfixes
    libxshmfence
    libxxf86vm
    llvm-libs
    lm_sensors
    wayland
    zlib
    zstd

    libomxil-bellagio
  )
  optdepends=("opengl-man-pages: for the OpenGL API man pages")
  provides=(
    mesa-libgl
    opengl-driver
  )
  conflicts=(mesa-libgl)
  replaces=(mesa-libgl)

  meson install -C build --destdir "$pkgdir"

  (
    local libdir=usr/lib icddir=usr/share/vulkan/icd.d

  cd $pkgdir
  
    _pick libva $libdir/dri/*_drv_video.so

    _pick vdpau $libdir/vdpau

    _pick clover $libdir/gallium-pipe
    _pick clover $libdir/libMesaOpenCL*
    _pick clover etc/OpenCL/vendors/mesa.icd

    # _pick clrust $libdir/libRusticlOpenCL*
    # _pick clrust etc/OpenCL/vendors/rusticl.icd
    #
    _pick vklayer $libdir/libVkLayer_*.so
    _pick vklayer usr/bin/mesa-overlay-control.py
    _pick vklayer usr/share/vulkan/{ex,im}plicit_layer.d

    # _pick vknvidia $icddir/nouveau_*.json
    # _pick vknvidia $libdir/libvulkan_nouveau*.so
    #
    # _pick vkradeon $icddir/radeon_icd*.json
    # _pick vkradeon $libdir/libvulkan_radeon.so
    # _pick vkradeon usr/share/drirc.d/00-radv-defaults.conf
    #
    _pick vkswrast $icddir/lvp_icd*.json
    _pick vkswrast $libdir/libvulkan_lvp.so

    _pick vkvirtio $icddir/virtio_icd*.json
    _pick vkvirtio $libdir/libvulkan_virtio.so

    # _pick vkbroadcom $icddir/broadcom_icd*.json
    # _pick vkbroadcom $libdir/libvulkan_broadcom.so
    #
    # _pick vkpanfrost $icddir/panfrost_icd*.json
    # _pick vkpanfrost $libdir/libvulkan_panfrost.so
    #
    _pick vkfreedreno $icddir/freedreno_icd*.json
    _pick vkfreedreno $libdir/libvulkan_freedreno.so

    # indirect rendering
    ln -sr $libdir/libGLX_{mesa,indirect}.so.0
  )

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_libva-mesa-driver() {
  pkgdesc="Open-source VA-API drivers"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm
    libelf
    libx11
    libxcb
    libxshmfence
    llvm-libs
    zlib
    zstd
  )
  provides=(libva-driver)

  mv libva/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_mesa-vdpau() {
  pkgdesc="Open-source VDPAU drivers"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm
    libelf
    libx11
    libxcb
    libxshmfence
    llvm-libs
    zlib
    zstd
  )
  provides=(vdpau-driver)

  mv vdpau/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_opencl-clover-mesa() {
  pkgdesc="Open-source OpenCL drivers - Clover variant"
  depends=(
    clang
    expat
    gcc-libs
    glibc
    libdrm
    libelf
    llvm-libs
    spirv-llvm-translator
    spirv-tools
    zlib
    zstd

    libclc
  )
  optdepends=("opencl-headers: headers necessary for OpenCL development")
  provides=(opencl-driver)
  replaces=("opencl-mesa<=23.1.4-1")
  conflicts=(opencl-mesa)

  mv clover/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# package_opencl-rusticl-mesa() {
#   pkgdesc="Open-source OpenCL drivers - RustICL variant"
#   depends=(
#     clang
#     expat
#     gcc-libs
#     glibc
#     libdrm
#     libelf
#     llvm-libs
#     spirv-llvm-translator
#     spirv-tools
#     zlib
#     zstd
#
#     libclc
#   )
#   optdepends=("opencl-headers: headers necessary for OpenCL development")
#   provides=(opencl-driver)
#   replaces=("opencl-mesa<=23.1.4-1")
#   conflicts=(opencl-mesa)
#
#   mv clrust/* "$pkgdir"
#
#   install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
# }
#
package_vulkan-mesa-layers() {
  pkgdesc="Mesa's Vulkan layers"
  depends=(
    gcc-libs
    glibc
    libdrm
    libxcb
    wayland

    python
  )
  conflicts=(vulkan-mesa-layer)
  replaces=(vulkan-mesa-layer)

  mv vklayer/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# package_vulkan-nouveau() {
#   pkgdesc="Open-source Vulkan driver for Nvidia GPUs"
#   depends=(
#     expat
#     gcc-libs
#     glibc
#     libdrm
#     libx11
#     libxcb
#     libxshmfence
#     systemd-libs
#     vulkan-icd-loader
#     wayland
#     xcb-util-keysyms
#     zlib
#     zstd
#   )
#   optdepends=("vulkan-mesa-layers: additional vulkan layers")
#   provides=(vulkan-driver)
#
#   mv vknvidia/* "$pkgdir"
#
#   install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
# }
#
# package_vulkan-radeon() {
#   pkgdesc="Open-source Vulkan driver for AMD GPUs"
#   depends=(
#     expat
#     gcc-libs
#     glibc
#     libdrm
#     libelf
#     libx11
#     libxcb
#     libxshmfence
#     llvm-libs
#     systemd-libs
#     vulkan-icd-loader
#     wayland
#     xcb-util-keysyms
#     zlib
#     zstd
#   )
#   optdepends=("vulkan-mesa-layers: additional vulkan layers")
#   provides=(vulkan-driver)
#
#   mv vkradeon/* "$pkgdir"
#
#   install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
# }
#
package_vulkan-swrast() {
  pkgdesc="Open-source Vulkan driver for CPUs (Software Rasterizer)"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm
    libx11
    libxcb
    libxshmfence
    llvm-libs
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=("vulkan-mesa-layers: additional vulkan layers")
  conflicts=(vulkan-mesa)
  replaces=(vulkan-mesa)
  provides=(vulkan-driver)

  mv vkswrast/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_vulkan-virtio() {
  pkgdesc="Open-source Vulkan driver for Virtio-GPU (Venus)"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm
    libx11
    libxcb
    libxshmfence
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=("vulkan-mesa-layers: additional vulkan layers")
  provides=(vulkan-driver)

  mv vkvirtio/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# package_vulkan-broadcom() {
#   pkgdesc="Broadcom's Vulkan mesa driver"
#   depends=(
#     wayland
#     libx11
#     libxshmfence
#     libdrm
#   )
#   optdepends=("vulkan-mesa-layers: additional vulkan layers")
#   provides=(vulkan-driver)
#
#   mv vkbroadcom/* "$pkgdir"
#
#   install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
# }
#
# package_vulkan-panfrost() {
#   pkgdesc="Panfrost Vulkan mesa driver"
#   depends=(
#     wayland
#     libx11
#     libxshmfence
#     libdrm
#   )
#   optdepends=("vulkan-mesa-layers: additional vulkan layers")
#   provides=(vulkan-driver)
#
#   mv vkpanfrost/* "$pkgdir"
#
#   install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
# }
#
package_vulkan-freedreno() {
  pkgdesc="Freedreno Vulkan mesa driver"
  depends=(
    wayland
    libx11
    libxshmfence
    libdrm
  )
  optdepends=("vulkan-mesa-layers: additional vulkan layers")
  provides=(vulkan-driver)

  mv vkfreedreno/* "$pkgdir"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
